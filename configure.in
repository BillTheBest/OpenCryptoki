dnl Process this file with autoconf to produce a configure script.
AC_INIT([openCryptoki], [2.3.1], [opencryptoki-tech@lists.sourceforge.net])

dnl Backup CFLAGS so that once the auto tools set it to "-g -O2",
dnl (which we want to avoid), we can restore them to whatever the
dnl user might have wanted below.
cmdline_CFLAGS="$CFLAGS"

# Compute $target
AC_CANONICAL_TARGET

AM_INIT_AUTOMAKE([foreign 1.6])

dnl Checks for programs.

dnl Checks for libraries.

dnl Checks for header files.
AC_DISABLE_STATIC
AC_PROG_LIBTOOL

AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h limits.h strings.h sys/file.h syslog.h unistd.h])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_STRUCT_TM
dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_FUNC_MEMCMP
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([getcwd])

dnl Used in various scripts
AC_PATH_PROG([ID], [id], [/us/bin/id])
AC_PATH_PROG([USERMOD], [usermod], [/usr/sbin/usermod])
AC_PATH_PROG([GROUPADD], [groupadd], [/usr/sbin/groupadd])
AC_PATH_PROG([CAT], [cat], [/bin/cat])
AC_PATH_PROG([CHMOD], [chmod], [/bin/chmod])
AC_PATH_PROG([CHGRP], [chgrp], [/bin/chgrp])
AC_PROG_SED
AC_PROG_MKDIR_P

CFLAGS="$cmdline_CFLAGS"

s390=0

# Arch specific stuff
case $target in
	*s390x*)
		s390=1
		AM_CONDITIONAL(S390, true) ;;
	*s390*)
		s390=1
		AM_CONDITIONAL(S390, true) ;;
        *)
		AM_CONDITIONAL(S390, false) ;;
esac

dnl ---
dnl --- Check all --enable/--disable-features
dnl ---

dnl --- Debugging support
AC_ARG_ENABLE([debug],
	AC_HELP_STRING([--enable-debug=no/yes],
	[enable debugging build [[default=no]]]),
	AC_MSG_NOTICE([**** Requested enable_debug=$enable_debug]),
	[enable_debug=no])

dnl --- build testcases
AC_ARG_ENABLE([testcases],
	AC_HELP_STRING([--enable-testcases=no/yes],
	[build the test cases [[default=no]]]),
	AC_MSG_NOTICE([**** Requested enable_testcases=$enable_testcases]),
	[enable_testcases=no])

dnl --- Check if building daemon
AC_ARG_ENABLE([daemon],
	AC_HELP_STRING([--enable-daemon=no/yes],
	[build pkcsslotd daemon [[default=yes]]]),
	AC_MSG_NOTICE([**** Requested enable_daemon=$enable_daemon]),
	[enable_daemon=yes])

dnl --- Check if building library
AC_ARG_ENABLE([library],
	AC_HELP_STRING([--enable-library=no/yes],
	[build opencryptoki libraries [[default=yes]]]),
	AC_MSG_NOTICE([**** Requested enable_library=$enable_library]),
	[enable_library=yes])

dnl --- Enable/disable tokens

dnl --- ICA token
AC_ARG_ENABLE([icatok],
	AC_HELP_STRING([--enable-icatok=no/yes],
	[build ica token [[default=yes]]]),
	AC_MSG_NOTICE([**** Requested enable_icatok=$enable_icatok]),
	[enable_icatok=yes])

dnl --- CCA token
AC_ARG_ENABLE([ccatok],
	AC_HELP_STRING([--with-ccatok=no/yes],
	[build cca token [[default=yes]]]),
	AC_MSG_NOTICE([**** Requested enable_ccatok=$enable_ccatok]),
	[enable_ccatok=yes])

dnl --- software token
AC_ARG_ENABLE([swtok],
	AC_HELP_STRING([--with-swtok=no/yes],
	[build software token [[default=yes]]]),
	AC_MSG_NOTICE([**** Requested enable_swtok=$enable_swtok]),
	[enable_swtok=yes])

dnl --- TPM token
AC_ARG_ENABLE([tpmtok],
	AC_HELP_STRING([--enable-tpmtok=no/yes],
	[build tpm token (Trusted Platform Module hardware, requires TSS development libraries) [[default=yes]]]),
	AC_MSG_NOTICE([**** Requested enable_tpmtok=$enable_tpmtok]),
	[enable_tpmtok=yes])

dnl --- icc token (IBM 4758 PCICC hardware)
AC_ARG_ENABLE([icctok],
	AC_HELP_STRING([--enable-icctok=no/yes],
	[build icc token (requires SCC development libraries) [[default=no]]]),
	AC_MSG_NOTICE([**** Requested enable_icctok=$enable_icctok]),
	[enable_icctok=no])

dnl --- aep token (AEP Crypto Accelerator hardware)
AC_ARG_ENABLE([aeptok],
	AC_HELP_STRING([--enable-aeptok=no/yes],
	[build aep token (AEP crypto Accelerator, requires AEP development libraries) [[default=no]]]),
	AC_MSG_NOTICE([**** Requested enable_aeptok=$enable_aeptok]),
	[enable_aeptok=no])

dnl --- bcom token (Broadcom Crypto Accelerator hardware)
AC_ARG_ENABLE([bcomtok],
	AC_HELP_STRING([--enable-bcomtok=no/yes],
	[build bcom token (Broadcom Crypto Accelerator, requires uBSec development libraries) [[default=no]]]),
	AC_MSG_NOTICE([**** Requested enable_bcomtok=$enable_bcomtok]),
	[enable_bcomtok=no])

dnl -- cr token (Corrent Crypto Accelerator hardware)
AC_ARG_ENABLE([crtok],
	AC_HELP_STRING([--enable-crtok=no/yes],
	[build cr token (Corrent Crypto Accelerator, requires Socketarmor development libraries) [[default=no]]]),
	AC_MSG_NOTICE([**** Requested enable_crtok=$enable_crtoken]),
	[enable_crtok=no])

dnl --- token-specific stuff

dnl --- pkcscca_migrate
AC_ARG_ENABLE([pkcscca_migrate],
	AC_HELP_STRING([--enable-pkcscca_migrate=no/yes],
	[build pkcscca_migrate - A tool to migrate CCA tokens from one Master key to another [[default=no]]]),
	AC_MSG_NOTICE([**** Requested enable_pkcscca_migrate=$enable_pkcscca_migrate]),
	[enable_pkcscca_migrate=no])

dnl ---
dnl --- Check for external software
dnl ---

dnl --- Openssl development files
AC_ARG_WITH([openssl],
	AC_HELP_STRING([--with-openssl[[=DIR]]], [openssl location]),
	AC_MSG_NOTICE([**** Requested with_openssl=$with_openssl]),
	[with_openssl=check])

dnl --- Libica development files
AC_ARG_WITH([libica],
	AC_HELP_STRING([--with-libica[[=DIR]]], [libica location]),
	AC_MSG_NOTICE([**** Requested with_libica=$with_libica]),
	[with_libica=check])

dnl --- TSS (TrouSerS) development files
AC_ARG_WITH([tss],
	AC_HELP_STRING([--with-tss[[=DIR]]], [TSS (TrouSerS) library location]),
	AC_MSG_NOTICE([**** Requested with_tss=$with_tss]),
	[with_tss=check])

dnl --- SCC development files (IBM 4758 PCI Cryptographic Coprocessor)
AC_ARG_WITH([scc],
	AC_HELP_STRING([--with-scc[[=DIR]]], [SCC library (IBM 4758 hardware SDK) location]),
	AC_MSG_NOTICE([**** Requested with_scc=$with_scc]),
	[with_scc=check])

dnl --- AEP development files (AEP crypto hardware)
AC_ARG_WITH([aep],
	AC_HELP_STRING([--with-aep[[=DIR]]], [AEP library location]),
	AC_MSG_NOTICE([**** Requested with_aep=$with_aep]),
	[with_aep=check])

dnl --- uBSec development files (Broadcom crypto hardware)
AC_ARG_WITH([ubsec],
	AC_HELP_STRING([--with-ubsec[[=DIR]]], [uBSec library (Broadcom hardware SDK) location]),
	AC_MSG_NOTICE([**** Requested with_ubsec=$with_ubsec]),
	[with_ubsec=check])

dnl --- Socketarmor development files (Corrent crypto hardware)
AC_ARG_WITH([socketarmor],
	AC_HELP_STRING([--with-socketarmor[[=DIR]]], [Socketarmor library (Corrent hardware SDK) location]),
	AC_MSG_NOTICE([**** Requested with_socketarmor=$with_socketarmor]),
	[with_socketarmor=check])

dnl --- xcryptolinz development files (IBM CCA development files)
AC_ARG_WITH([xcryptolinz],
	AC_HELP_STRING([--with-xcryptolinz[[=DIR]]], [xcryptolinz (IBM CCA suppor library) location]),
	AC_MSG_NOTICE([**** Requested with_xcryptolinz=$with_xcryptolinz]),
	[with_xcryptolinz=check])

dnl ---
dnl ---
dnl --- Now that we have all the options, let's check for a valid build
dnl ---

dnl --- first, check what external software is present or specified
dnl --- with --with-package=DIR

dnl --- with_openssl
OPENSSL_CFLAGS=
OPENSSL_LIBS=
if test "x$with_openssl" != "xno"; then
	if test "x$with_openssl" != "xyes" -a "x$with_openssl" != "xcheck"; then
		OPENSSL_CFLAGS="-I$with_openssl"
		OPENSSL_LIBS="-L$with_openssl"
	fi
	old_cflags="$CFLAGS"
	old_libs="$LIBS"
	CFLAGS="$CFLAGS $OPENSSL_CFLAGS"
	LIBS="$LIBS $OPENSSL_LIBS"
	AC_CHECK_HEADER([openssl/ssl.h], [], [
		if test "x$with_openssl" != "xcheck"; then
			AC_MSG_ERROR([Build with OpenSSL requested but OpenSSL headers couldn't be found])
		fi
		with_openssl=no
	])
	if test "x$with_openssl" != "xno"; then
		AC_CHECK_LIB([crypto], [RSA_generate_key], [
			OPENSSL_LIBS="$OPENSSL_LIBS -lcrypto"
			with_openssl=yes
			], [
				if test "x$with_openssl" != "xcheck"; then
					AC_MSG_ERROR([Build with OpenSSL requested but OpenSSL libraries couldn't be found])
				fi
				with_openssl=no
		])
	fi
	if test "x$with_openssl" = "xno"; then
		CFLAGS="$old_cflags"
		LIBS="$old_libs"
	fi
fi
AC_SUBST([OPENSSL_CFLAGS])
AC_SUBST([OPENSSL_LIBS])


dnl --- with_libica
LIBICA_CFLAGS=
LIBICA_LIBS=
if test "x$with_libica" != "xno"; then
	if test "x$with_libica" != "xyes" -a "x$with_libica" != "xcheck"; then
		LIBICA_CFLAGS="-I$with_libica"
		LIBICA_LIBS="-L$with_libica"
	fi
	old_cflags="$CFLAGS"
	old_libs="$LIBS"
	CFLAGS="$CFLAGS $LIBICA_CFLAGS"
	LIBS="$LIBS $LIBICA_LIBS"
	AC_CHECK_HEADER([ica_api.h], [], [
		if test "x$with_libica" != "xcheck"; then
			AC_MSG_ERROR([Build with Libica requested but Libica headers couldn't be found])
		fi
		with_libica=no
	])
	if test "x$with_libica" != "xno"; then
		AC_CHECK_LIB([ica], [ica_open_adapter],
			[with_libica=yes], [
				if test "x$with_libica" != "xcheck"; then
					AC_MSG_ERROR([Build with Libica requested but Libica libraries (v 2.x or higher) couldn't be found])
				fi
				with_libica=no
		])
	fi
	if test "x$with_libica" = "xno"; then
		CFLAGS="$old_cflags"
		LIBS="$old_libs"
	fi
fi
AC_SUBST([LIBICA_CFLAGS])
AC_SUBST([LIBICA_LIBS])

dnl --- with_tss
TSS_CFLAGS=
TSS_LIBS=
if test "x$with_tss" != "xno"; then
	if test "x$with_tss" != "xyes" -a "x$with_tss" != "xcheck"; then
		TSS_CFLAGS="-I$with_tss"
		TSS_LIBS="-L$with_tss"
	fi
	old_cflags="$CFLAGS"
	old_libs="$LIBS"
	CFLAGS="$CFLAGS $TSS_CFLAGS"
	LIBS="$LIBS $TSS_LIBS"
	AC_CHECK_HEADER([tss/platform.h], [], [
		if test "x$with_tss" != "xcheck"; then
			AC_MSG_ERROR([Build with TSS requested but TSS headers couldn't be found])
		fi
		with_tss=no
	])
	if test "x$with_tss" != "xno"; then
		AC_CHECK_LIB([tspi], [Tspi_Context_Create],
			[with_tss=yes], [
				if test "x$with_tss" != "xcheck"; then
					AC_MSG_ERROR([Build with TSS requested but TSS libraries couldn't be found])
				fi
				with_tss=no
		])
	fi
	if test "x$with_tss" = "xno"; then
		CFLAGS="$old_cflags"
		LIBS="$old_libs"
	fi
fi
AC_SUBST([TSS_CFLAGS])
AC_SUBST([TSS_LIBS])

dnl --- with_scc
SCC_CFLAGS=
SCC_LIBS=
if test "x$with_scc" != "xno"; then
	if test "x$with_scc" != "xyes" -a "x$with_scc" != "xcheck"; then
		SCC_CFLAGS="-I$with_scc"
		SCC_LIBS="-L$with_scc"
	fi
	old_cflags="$CFLAGS"
	old_libs="$LIBS"
	CFLAGS="$CFLAGS $SCC_CFLAGS"
	LIBS="$LIBS $SCC_LIBS"
	AC_CHECK_HEADER([scc_host.h], [], [
		if test "x$with_scc" != "xcheck"; then
			AC_MSG_ERROR([Build with SCC requested but SCC headers couldn't be found])
		fi
		with_scc=no
	])
	if test "x$with_scc" != "xno"; then
		AC_CHECK_LIB([scc], [sccOpenAdapter],
			[with_scc=yes], [
				if test "x$with_scc" != "xcheck"; then
					AC_MSG_ERROR([Build with SCC requested but SCC libraries couldn't be found])
				fi
				with_scc=no
		])
	fi
	if test "x$with_scc" = "xno"; then
		CFLAGS="$old_cflags"
		LIBS="$old_libs"
	fi
fi
AC_SUBST([SCC_CFLAGS])
AC_SUBST([SCC_LIBS])

dnl --- with_aep
AEP_LIBS=
if test "x$with_aep" != "xno"; then
	if test "x$with_aep" != "xyes" -a "x$with_aep" != "xcheck"; then
		AEP_LIBS="-L$with_aep"
	fi
	old_libs="$LIBS"
	LIBS="$LIBS $AEP_LIBS"
	AC_CHECK_LIB([aep], [AEP_Initialize],
		[with_aep=yes], [
			if test "x$with_aep" != "xcheck"; then
				AC_MSG_ERROR([Build with AEP requested but AEP libraries couldn't be found])
			fi
			with_aep=no
	])
	if test "x$with_aep" = "xno"; then
		CFLAGS="$old_cflags"
		LIBS="$old_libs"
	fi
fi
AC_SUBST([AEP_LIBS])

dnl --- with_ubsec
UBSEC_CFLAGS=
UBSEC_LIBS=
if test "x$with_ubsec" != "xno"; then
	if test "x$with_ubsec" != "xyes" -a "x$with_ubsec" != "xcheck"; then
		OPENSSL_CFLAGS="-I$with_ubsec"
		OPENSSL_LIBS="-L$with_ubsec"
	fi
	old_cflags="$CFLAGS"
	old_libs="$LIBS"
	CFLAGS="$CFLAGS $UBSEC_CFLAGS"
	LIBS="$LIBS $UBSEC_LIBS"
	AC_CHECK_HEADER([ubsec.h], [], [
		if test "x$with_ubsec" != "xcheck"; then
			AC_MSG_ERROR([Build with uBSec requested but uBSec headers couldn't be found])
		fi
		with_ubsec=no
	])
	if test "x$with_ubsec" != "xno"; then
		AC_CHECK_LIB([ubsec], [ubsec_open],
			[with_ubsec=yes], [
				if test "x$with_ubsec" != "xcheck"; then
					AC_MSG_ERROR([Build with uBSec requested but uBSec libraries couldn't be found])
				fi
				with_ubsec=no
		])
	fi
	if test "x$with_ubsec" = "xno"; then
		CFLAGS="$old_cflags"
		LIBS="$old_libs"
	fi
fi
AC_SUBST([UBSEC_CFLAGS])
AC_SUBST([UBSEC_LIBS])

dnl --- with_socketarmor
SOCKETARMOR_CFLAGS=
SOCKETARMOR_LIBS=
if test "x$with_socketarmor" != "xno"; then
	if test "x$with_socketarmor" != "xyes" -a "x$with_socketarmor" != "xcheck"; then
		SOCKETARMOR_CFLAGS="-I$with_socketarmor"
		SOCKETARMOR_LIBS="-L$with_socketarmor"
	fi
	old_cflags="$CFLAGS"
	old_libs="$LIBS"
	CFLAGS="$CFLAGS $SOCKETARMOR_CFLAGS"
	LIBS="$LIBS $SOCKETARMOR_LIBS"
	AC_CHECK_HEADER([typhoon.h], [], [
		if test "x$with_socketarmor" != "xcheck"; then
			AC_MSG_ERROR([Build with Socketarmor requested but Socketarmor headers couldn't be found])
		fi
		with_socketarmor=no
	])
	if test "x$with_socketarmor" != "xno"; then
		AC_CHECK_LIB([socketarmor], [CR_init_lib],
			[with_socketarmor=yes], [
				if test "x$with_socketarmor" != "xcheck"; then
					AC_MSG_ERROR([Build with Socketarmor requested but Socketarmor libraries couldn't be found])
				fi
				with_socketarmor=no
		])
	fi
	if test "x$with_socketarmor" = "xno"; then
		CFLAGS="$old_cflags"
		LIBS="$old_libs"
	fi
fi
AC_SUBST([SOCKETARMOR_CFLAGS])
AC_SUBST([SOCKETARMOR_LIBS])

dnl --- with_xcryptolinz
XCRYPTOLINZ_CFLAGS=
XCRYPTOLINZ_LIBS=
if test "x$with_xcryptolinz" != "xno"; then
	if test "x$with_xcryptolinz" != "xyes" -a "x$with_xcryptolinz" != "xcheck"; then
		XCRYPTOLINZ_CFLAGS="-I$with_xcryptolinz"
		XCRYPTOLINZ_LIBS="-L$with_xcryptolinz"
	fi
	old_cflags="$CFLAGS"
	old_libs="$LIBS"
	CFLAGS="$CFLAGS $XCRYPTOLINZ_CFLAGS"
	LIBS="$LIBS $XCRYPTOLINZ_LIBS"
dnl - The above may not be necessary since opencryptoki brings this header file anyway.
	AC_CHECK_HEADER([csulincl.h], [], [
		if test "x$with_xcryptolinz" != "xcheck"; then
			AC_MSG_ERROR([Build with xcryptolinz requested but xcryptolinz headers couldn't be found])
		fi
		with_xcryptolinz=no
	])
	if test "x$with_xcryptolinz" != "xno"; then
		AC_CHECK_LIB([csulsapi], [CSNBKTC],
			[with_xcryptolinz=yes], [
				if test "x$with_xcryptolinz" != "xcheck"; then
					AC_MSG_ERROR([Build with xcryptolinz requested but xcryptolinz libraries couldn't be found])
				fi
				with_xcryptolinz=no
		])
	fi
	if test "x$with_xcryptolinz" = "xno"; then
		CFLAGS="$old_cflags"
		LIBS="$old_libs"
	fi
fi
AC_SUBST([XCRYPTOLINZ_CFLAGS])
AC_SUBST([XCRYPTOLINZ_LIBS])


dnl ---
dnl --- Now check enabled features, while making sure every required
dnl --- package is available
dnl ---

dnl --- enable_debug
AM_CONDITIONAL([ENABLE_DEBUG], [test "x$enable_debug" = "xyes"])
if test "x$enable_debug" = "xyes"; then
	DEBUG_CFLAGS="-g -O0"
fi

dnl --- enable_testcases
if test "x$enable_testcases" = "xyes"; then
	AC_CHECK_PROG([HAVE_EXPECT], [expect], [yes], [no])

	if test "x$HAVE_EXPECT" = "xno"; then
		AC_MSG_ERROR([*** testcases requires 'expect' interpreter, which wasn't found])
                enable_testcases=no
	fi
fi
AM_CONDITIONAL([ENABLE_TESTCASES], [test "x$enable_testcases" = "xyes"])

dnl --- enable_daemon
AM_CONDITIONAL([ENABLE_DAEMON], [test "x$enable_daemon" = "xyes"])

dnl --- enable_library
AM_CONDITIONAL([ENABLE_LIBRARY], [test "x$enable_library" = "xyes"])

dnl --- enable_icatok
if test "x$enable_icatok" != "xno"; then
	if test "x$with_libica" != "xyes"; then
		AC_MSG_ERROR([ica token build requested but libica development files not found])
		enable_icatok=no
	fi
fi
AM_CONDITIONAL([ENABLE_ICATOK], test "x$enable_icatok" = "xyes")

dnl --- enable_ccatok
AM_CONDITIONAL([ENABLE_CCATOK], test "x$enable_ccatok" = "xyes")

dnl --- enable_swtok
if test "x$enable_swtok" != "xno"; then
	if test "x$with_openssl" != "xyes"; then
		AC_MSG_ERROR([software token build requested but openssl development files not found])
		enable_swtok=no
	fi
fi
AM_CONDITIONAL([ENABLE_SWTOK], test "x$enable_swtok" = "xyes")

dnl --- enable_tpmtok
if test "x$enable_tpmtok" != "xno"; then
	if test "x$with_tss" != "xyes"; then
		AC_MSG_ERROR([tpm token build requested but TSS development files not found])
		enable_tpmtok=no
	fi
fi
AM_CONDITIONAL([ENABLE_TPMTOK], test "x$enable_tpmtok" = "xyes")

dnl --- enable_icctok
if test "x$enable_icctok" != "xno"; then
	if test "x$with_scc" != "xyes"; then
		AC_MSG_ERROR([icc token build requested but no scc development files not found])
		enable_icctok=no
	fi
fi
AM_CONDITIONAL([ENABLE_ICCTOK], test "x$enable_icctok" = "xyes")

dnl --- enable_aeptok
if test "x$enable_aeptok" != "xno"; then
	if test "x$with_aep" != "xyes"; then
		AC_MSG_ERROR([aep token build requested but no aep development files not found])
		enable_aeptok=no
	fi
fi
AM_CONDITIONAL([ENABLE_AEPTOK], test "x$enable_aeptok" = "xyes")

dnl --- enable_bcomtok
if test "x$enable_bcomtok" != "xno"; then
	if test "x$with_ubsec" != "xyes"; then
		AC_MSG_ERROR([bcom token build requested but no uBSec development files not found])
		enable_bcomok=no
	fi
fi
AM_CONDITIONAL([ENABLE_BCOMTOK], test "x$enable_bcomtok" = "xyes")

dnl --- enable_crtok
if test "x$enable_crtok" != "xno"; then
	if test "x$with_socketarmor" != "xyes"; then
		AC_MSG_ERROR([cr token build requested but no Socketarmor development files not found])
		enable_crtok=no
	fi
fi
AM_CONDITIONAL([ENABLE_CRTOK], test "x$enable_crtok" = "xyes")

dnl --- enable_pkcscca_migrate
if test "x$enable_pkcscca_migrate" != "xno"; then
	if test "x$with_xcryptolinz" != "xyes"; then
		AC_MSG_ERROR([pkcscca_migrate build requested but no xcryptolinz libraries found])
		enable_pkcscca_migrate=no
	fi
fi
AM_CONDITIONAL([ENABLE_PKCSCCA_MIGRATE], test "x$enable_pkcscca_migrate" = "xyes")


CFLAGS="$CFLAGS $DEBUG_CFLAGS -DPKCS64 -D_XOPEN_SOURCE=500"

CFLAGS+=' -DCONFIG_PATH=\"$(localstatedir)/lib/opencryptoki\" \
	-DSBIN_PATH=\"$(sbindir)\" -DLIB_PATH=\"$(libdir)\"'

# At this point, CFLAGS is set to something sensible
AC_PROG_CC

AC_OUTPUT([Makefile usr/Makefile \
          usr/include/Makefile \
          usr/include/pkcs11/Makefile \
          usr/lib/Makefile \
          usr/lib/pkcs11/Makefile \
          usr/lib/pkcs11/api/Makefile \
          usr/lib/pkcs11/api/shrd_mem.c \
          usr/lib/pkcs11/ica_stdll/Makefile \
          usr/lib/pkcs11/ica_s390_stdll/Makefile \
          usr/sbin/Makefile \
          usr/sbin/pkcsslotd/Makefile \
          usr/sbin/pkcs11_startup/Makefile \
          usr/sbin/pkcs_slot/Makefile \
          usr/sbin/pkcsconf/Makefile \
          usr/sbin/pkcscca_migrate/Makefile \
          usr/lib/pkcs11/methods/Makefile \
          usr/lib/pkcs11/leeds_stdll/Makefile \
          usr/lib/pkcs11/soft_stdll/Makefile \
          usr/lib/pkcs11/bcom_stdll/Makefile \
          usr/lib/pkcs11/cr_stdll/Makefile \
          usr/lib/pkcs11/aep_stdll/Makefile \
          usr/lib/pkcs11/tpm_stdll/Makefile \
          usr/lib/pkcs11/cca_stdll/Makefile \
          usr/lib/pkcs11/methods/4758_status/Makefile \
          misc/Makefile \
	  testcases/Makefile \
	  testcases/common/Makefile \
	  testcases/driver/Makefile \
	  testcases/init_tok/Makefile \
	  testcases/mkobj/Makefile \
	  testcases/oc-digest/Makefile \
	  testcases/rsa_keygen/Makefile \
	  testcases/rsa_test/Makefile \
	  testcases/speed/Makefile \
	  testcases/test_crypto/Makefile \
	  testcases/threadmkobj/Makefile \
	  testcases/tok_obj/Makefile \
	  testcases/v2.11/Makefile \
	  testcases/login/Makefile \
	  man/Makefile \
	  man/man1/Makefile \
	  man/man1/pkcsconf.1 \
	  man/man1/pkcs11_startup.1 \
	  man/man5/Makefile \
	  man/man5/pk_config_data.5 \
	  man/man7/Makefile \
	  man/man7/opencryptoki.7 \
	  man/man8/Makefile \
	  man/man8/pkcsslotd.8])

echo
echo "CLFAGS=$CFLAGS"
echo

